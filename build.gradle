plugins {
    id 'java'
    id 'application'
    id 'jsonschema2pojo'
    id 'com.github.jk1.dependency-license-report' version '2.4'
    id 'com.github.seanrl.jaxb' version '2.5.4'
}

group = 'com.github.stuartraetaylor.punkapiexport'
version = '0.2.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

String jaxbVersion = '4.0.+'
String jacksonVersion = '2.16.+'

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.22.+'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.+'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.+'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.6.+'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion

    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: jaxbVersion

    runtimeOnly group: 'com.sun.xml.bind', name: 'jaxb-impl', version: jaxbVersion

    jaxb group: 'org.glassfish.jaxb', name: 'jaxb-xjc', version: jaxbVersion
    jaxb group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbVersion
}

defaultTasks 'build'

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '8.5'
}

jar {
    archiveBaseName = 'pinkapiexport'
}

processResources {
    doLast {
        ant.propertyfile(file: "${project.sourceSets.main.output.resourcesDir}/version-info.properties") {
            entry(key: 'version', value: project.version)
        }
    }
}

application {
    mainClass = "${group}.Main"
}

jsonSchema2Pojo {
    source = files('submodules/punkapi-db/spec/schema.json')
    targetDirectory = file("${project.buildDir}/generated-sources/js2p")
    removeOldOutput = false

    targetPackage = "${group}.punkapi.model"
    classNamePrefix = 'Punk'

    includeGetters = true
    includeSetters = true
    useBigDecimals = true
}

jaxb {
    xsdDir = "${project.projectDir}/submodules/beerxml-schema/historical/BeerXML v1/"
    xjc {
        generatePackage = "${group}.beerxml.model"
    }
}

// Add generated JAXB classes to source set.
project.sourceSets.main.java.srcDirs += [ file(jaxb.xjc.destinationDir) ]
compileJava.dependsOn 'xjc'
xjc.dependsOn 'processResources'

licenseReport {
    renderers = [ new com.github.jk1.license.render.SimpleHtmlReportRenderer('license-report.html') ]
    filters = [ new com.github.jk1.license.filter.LicenseBundleNormalizer() ]
}
